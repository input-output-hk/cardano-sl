{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE TypeOperators       #-}

-- | Allows to automatically generate custom swagger documentation.

module CustomSwagger
    ( HasCustomSwagger (..)
    , toCustomSwagger
    ) where

import           Universum

import           Data.Swagger    (Swagger)
import           Servant         ((:<|>))
import           Servant.Swagger (HasSwagger (..))

-- | Allows to provide some extra info about given endpoint, in addition to info
-- generated by servant-swagger library.
class HasCustomSwagger api where
    swaggerModifier :: Proxy api -> Swagger -> Swagger

instance (HasCustomSwagger a, HasCustomSwagger b) => HasCustomSwagger (a :<|> b) where
    swaggerModifier _ = swaggerModifier (Proxy @a) . swaggerModifier (Proxy @b)

toCustomSwagger :: (HasSwagger api, HasCustomSwagger api) => Proxy api -> Swagger
toCustomSwagger = flip swaggerModifier =<< toSwagger
